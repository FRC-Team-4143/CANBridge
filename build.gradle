plugins {
  id 'c'
  id 'cpp'
  id 'java'
  id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.1'
  id 'edu.wpi.first.NativeUtils' version '2020.4.1'
  id 'edu.wpi.first.GradleJni' version '0.9.1'
  id 'edu.wpi.first.GradleVsCode' version '0.9.6'
  id 'google-test-test-suite'
}

allprojects {
    repositories {
        mavenCentral()
    }
    if (project.hasProperty('releaseMode')) {
        wpilibRepositories.addAllReleaseRepositories(it)
    } else {
        wpilibRepositories.addAllDevelopmentRepositories(it)
    }
}

apply from: 'config.gradle'

apply from: 'dependencies.gradle'

nativeUtils {
  exportsConfigs {
      CANBridge {
      }
  }
  privateExportsConfigs {
    CandleLib {
      exportsFile = project.file("src/main/native/c/candlelib/symbols.txt")
    }
  }
  privateExportsConfigs {
    SerialLib {
      exportsFile = project.file("src/main/native/cpp/serial/symbols.txt")
    }
  }

}

model {
  //libraries {
  //    candle_dll(NativeLib) {
  //    libraryName = 'candle_dll'
  //    targetPlatform = 'windowsx86-64'
  //    sharedMatchers << '**/*.lib'
  //    dynamicMatchers << '**/*.dll'
  //    file = project.file('lib/candle_dll/')
  //    }
  //}
  components {
    CANBridge(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp'
            include '**/*.cpp'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include'
          }
        }
      }
      binaries.all {
        lib library: "CandleLib", linkage: 'static'
        lib library: "SerialLib", linkage: 'static'
        if (it.targetPlatform.name == nativeUtils.wpi.platforms.roborio) {
          it.buildable = false
        }
      }

      nativeUtils.useRequiredLibrary(it, "wpilib_shared")
    }
    CandleLib(NativeLibrarySpec) {
      sources {
        c {
          source {
            srcDirs 'src/main/native/c/candlelib/'
            include '**/*.c'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include/candlelib'
          }
        }
      }
      binaries.all {
        if (it.targetPlatform.name == nativeUtils.wpi.platforms.roborio) {
          it.buildable = false
        }
      }
    }
    SerialLib(NativeLibrarySpec) {
      sources {
        cpp {
          source {
            srcDirs 'src/main/native/cpp/serial/'
            include '**/*.cc'
          }
          exportedHeaders {
            srcDirs 'src/main/native/include/serial'
          }
        }
      }
      binaries.all {
        if (it.targetPlatform.name == nativeUtils.wpi.platforms.roborio) {
          it.buildable = false
        }
      }
    }

  }

  testSuites {
    CANBridgeTest(GoogleTestTestSuiteSpec) {
      testing $.components.CANBridge
      sources.cpp {
        source {
          srcDir 'src/test/gtest/cpp'
          include '**/*.cpp'
        }
        exportedHeaders {
          srcDirs 'src/test/gtest/include/'
        }
      }

      binaries.all {
        lib library: "CandleLib", linkage: 'static'
        lib library: "SerialLib", linkage: 'static'
        if (it.targetPlatform.name == nativeUtils.wpi.platforms.roborio
            || it.targetPlatform.name == nativeUtils.wpi.platforms.raspbian
            || it.targetPlatform.name == nativeUtils.wpi.platforms.aarch64bionic) {
          it.buildable = false
        }
        // lib library: "SparkMaxDriver", linkage: 'shared'
      }
      nativeUtils.useRequiredLibrary(it, "wpilib_shared", "googletest_static")
    }
  }
}

apply from: 'publish.gradle'

wrapper {
  gradleVersion = '5.4.1'
}
